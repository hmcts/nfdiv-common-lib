plugins {
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'java-library'
    id 'maven-publish'
    id 'net.ltgt.apt' version '0.21'
}

group 'uk.gov.hmcts.reform'
version '1.2.5'

sourceCompatibility = 11
targetCompatibility = 11

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

checkstyle {
    maxWarnings = 0
    toolVersion = '8.22'

    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenCentral()
    jcenter()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def gitRepo = "https://github.com/hmcts/nfdiv-common-lib"

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/nfdiv-common-lib"
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'nfdiv-common-lib'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'No fault divorce common lib')
                root.appendNode('name', 'No fault divorce common lib')
                root.appendNode('url', gitRepo)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    //Run command with -PshouldPublish=false for checking whether version already exists
    boolean shouldPublish = "false" != findProperty('shouldPublish')
    publish = shouldPublish
    if (!shouldPublish) {
        println 'Will not publish artifact. Just check.'
    }

    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    pkg {
        repo = 'hmcts-maven'
        name = 'nfdiv-common-lib'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = gitRepo
        version {
            name = project.version
        }
    }
}

ext {
    commonsCollections4 = '4.1'
    commonsLang3 = '3.11'
    feignVersion = '10.2.3'
    feignFormVersion = '3.5.0'
    guavaVersion = '30.1-jre'
    hamcrest = '2.2'
    jsonPathAssert = '2.2.0'
    jacksonDatabind = '2.12.0'
    javaxValidationVersion = '2.0.0.Final'
    junit = '5.7.0'
    lombokVersion = '1.18.16'
    mapstruct = '1.2.0.Final'
    springCloudVersion = 'Greenwich.SR2'
    springVersion = '2.1.14.RELEASE'
    swaggerVersion = '1.5.20'
    wiremock = '2.24.1'
    wiremockJunit5 = '1.3.0'
}

ext["spring-cloud-openfeign.version"] = "2.1.2.RELEASE"

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    api group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
    api group: 'io.github.openfeign', name: 'feign-jackson', version: feignVersion
    api group: 'io.github.openfeign', name: 'feign-httpclient', version: feignVersion
    api group: 'io.github.openfeign.form', name: 'feign-form', version: feignFormVersion

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-migrationsupport', version: junit
    testImplementation group: 'org.hamcrest', name: 'hamcrest-core', version: hamcrest
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: hamcrest

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springVersion

    testImplementation group: 'ru.lanwen.wiremock', name: 'wiremock-junit5', version: wiremockJunit5

    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: wiremock
    testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock'
    testImplementation group: 'org.projectlombok', name: 'lombok', version: '1.16.16'
    testImplementation group: 'com.jayway.jsonpath', name: 'json-path-assert', version: jsonPathAssert

    implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.4'
    implementation group: 'com.deliveredtechnologies', name: 'rulebook-core', version: '0.9.1'
    implementation group: 'javax.validation', name: 'validation-api', version: javaxValidationVersion
    implementation group: 'io.swagger', name: 'swagger-annotations', version: swaggerVersion
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: commonsCollections4
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: commonsLang3
    implementation group: 'com.google.guava', name: 'guava', version: guavaVersion

    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonDatabind
    compileOnly group: 'org.mapstruct', name: 'mapstruct-processor', version: mapstruct
    annotationProcessor("org.mapstruct:mapstruct-processor:${mapstruct}")
    implementation group: 'org.mapstruct', name: 'mapstruct-jdk8', version: mapstruct
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

jar {
    enabled = true
}
